plugins {
    java
    `maven-publish`
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = "com.github.CatsT0day"
version = "1.0.0.8"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
    options.release.set(16)
}

repositories {
    mavenCentral()
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://jitpack.io")
    maven("https://lucko.me")
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT")
    implementation("net.kyori:adventure-text-minimessage:4.17.0")
    implementation("org.reflections:reflections:0.10.2") {
        exclude(group = "org.javassist", module = "javassist")
        exclude(group = "com.google.code.gson", module = "gson")
    }
    implementation("org.javassist:javassist:3.30.2-GA")
    compileOnly("net.luckperms:api:5.4")
    compileOnly("com.github.MilkBowl:VaultAPI:1.7")
}

tasks.shadowJar {
    archiveBaseName.set("EclipseAPI")
    archiveClassifier.set("")
    archiveVersion.set(project.version.toString())
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configure<PublishingExtension> {
    publications {
        register<MavenPublication>("maven") {
            groupId = "com.github.CatsT0day"
            artifactId = "EclipseAPI"
            version = project.version.toString()

            artifact(tasks.shadowJar)
        }
    }
    tasks.jar {
        enabled = false
    }
}
